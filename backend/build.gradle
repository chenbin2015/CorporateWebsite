plugins {
    id 'org.springframework.boot' version '3.3.2' apply false
    id 'io.spring.dependency-management' version '1.1.5' apply false
    id 'java'
}

allprojects {
    group = 'com.company.platform'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':common') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }
}

project(':security') {
    dependencies {
        implementation project(':common')
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    }
}

project(':repository') {
    dependencies {
        implementation project(':common')
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        runtimeOnly 'com.mysql:mysql-connector-j'
    }
}

project(':infrastructure') {
    dependencies {
        implementation project(':common')
    }
}

project(':service') {
    dependencies {
        implementation project(':common')
        implementation project(':repository')
        implementation project(':security')
    }
}

project(':admin-api') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':service')
        implementation project(':infrastructure')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    }
}

